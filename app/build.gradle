apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'io.fabric'

// get the first 8 chars of the latest git commit, and use it as the version build name
def appBuildPrefix =  ['sh', '-c', 'git rev-parse --short=8 HEAD'].execute().text.trim()

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"

    defaultConfig {
        applicationId "name.lmj0011.courierlocker"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 13
        versionName "0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        // ref: https://stackoverflow.com/a/48674264/2445763
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("bool", "DEBUG_MODE", "false")
            resValue("string", "app_build", appBuildPrefix)
        }
        debug {
            minifyEnabled false
            resValue("bool", "DEBUG_MODE", "true")
            resValue("string", "app_build", appBuildPrefix)
        }
    }
    flavorDimensions "default"
    productFlavors {
        dev { // active development/testing
            applicationIdSuffix ".dev"
            versionNameSuffix ".2.0-dev"
            resValue("string", "app_name", "Courier Locker (dev)")
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_dev",
                    appRoundIcon: "@mipmap/ic_launcher_dev_round"
            ]
        }
        beta { // public testing
            applicationIdSuffix ".beta"
            versionNameSuffix ".2.0-beta"
            resValue("string", "app_name", "Courier Locker (beta)")
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_beta",
                    appRoundIcon: "@mipmap/ic_launcher_beta_round"
            ]
        }
        prod { // official release
            applicationIdSuffix ".prod"
            versionNameSuffix ".2.0"
            resValue("string", "app_name", "Courier Locker")
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appRoundIcon: "@mipmap/ic_launcher_round"
            ]
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version_ktx"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version_ktx"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Room dependencies
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // FusedLocationProviderClient dependencies
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // GSON
    implementation 'com.google.code.gson:gson:2.8.5'

    // Lifecycle-aware components
    // ref: https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // useful for testing with mock data
    implementation 'com.github.moove-it:fakeit:v0.7'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutine"

    // HTTP lib
    implementation "com.github.kittinunf.fuel:fuel:$version_kittinunf_fuel"
    implementation "com.github.kittinunf.fuel:fuel-json:$version_kittinunf_fuel"

    // androidx.preference
    implementation "androidx.preference:preference:1.1.0-alpha04"

    // An adaptation of the JSR-310 backport for Android.
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    // Add the Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics:17.2.0'

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Shortbread
    implementation 'com.github.matthiasrobbers:shortbread:1.0.2'
    kapt 'com.github.matthiasrobbers:shortbread-compiler:1.0.2'
}


// ref: https://firebase.google.com/docs/android/setup#add-config-file
// Add the following line to the bottom of the file ??
apply plugin: 'com.google.gms.google-services'  // Google Play services Gradle plugin
